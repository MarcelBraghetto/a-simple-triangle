cmake_minimum_required(VERSION 3.4.1)

# Instruct our compiler that we want to use C++ 17 and use exceptions.
set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -std=c++17 -fexceptions")

# Determine what build type we are in, shifted to lower case (debug or release).
string(TOLOWER ${CMAKE_BUILD_TYPE} BUILD_VARIANT_DIR)

# Location of our third party libraries.
set(THIRD_PARTY_DIR "../../../third-party")

# Location of our own shared C++ code base.
set(MAIN_SOURCE_DIR "../../main/src")

# Location of the .so files generated by the 'sdl' module.
set(SDL_SO_DIR "${CMAKE_SOURCE_DIR}/../sdl/build/intermediates/ndkBuild/${BUILD_VARIANT_DIR}/obj/local/${ANDROID_ABI}")

# This is required to allow us to compile the Vulkan C++ header successfully.
add_definitions(-DVULKAN_HPP_TYPESAFE_CONVERSION)

# This is a dependency target for SDL, we will name it 'hidapi'.
add_library(
        hidapi
        SHARED
        IMPORTED
)

# This is the main SDL library target, we will name it 'sdl2'.
add_library(
        sdl2
        SHARED
        IMPORTED
)

# This is the SDL2 image library
add_library(
        sdl2_image
        SHARED
        IMPORTED
)

# This is the built in NDK logging library
find_library(log-lib log)

# We need to tell the C++ compiler where to find the .so (shared object) files for each of our targets. When the 'sdl' module is built, it will generate these .so files in its build folder. At the top of this file we created a field named SDL_SO_DIR which points at the build folder location where the .so files can be found.
set_target_properties(
        hidapi
        PROPERTIES
        IMPORTED_LOCATION
        ${SDL_SO_DIR}/libhidapi.so
)

set_target_properties(
        sdl2
        PROPERTIES
        IMPORTED_LOCATION
        ${SDL_SO_DIR}/libSDL2.so
)

set_target_properties(
        sdl2_image
        PROPERTIES
        IMPORTED_LOCATION
        ${SDL_SO_DIR}/libSDL2_image.so
)

# We will tell our build system where to look for third party header files.
include_directories(${THIRD_PARTY_DIR}/SDL/include)
include_directories(${THIRD_PARTY_DIR}/glm)
include_directories(${THIRD_PARTY_DIR}/tiny-obj-loader)
include_directories(${THIRD_PARTY_DIR}/SDL2_image)
include_directories(${ANDROID_NDK}/sources/third_party/vulkan/src/include)
include_directories(${ANDROID_NDK}/sources/third_party/vulkan/src/common)
include_directories(${CMAKE_SOURCE_DIR}/src/main/cpp)

# We will recursively collate all .hpp header and .cpp source files under the main source directory as the input for the compilation.
file(GLOB_RECURSE CPP_HEADERS ${MAIN_SOURCE_DIR}/*.hpp)
file(GLOB_RECURSE CPP_SOURCES ${MAIN_SOURCE_DIR}/*.cpp)

# The 'a-simple-triangle' library target is actually our own C++ code from the shared 'main' source code folder.
add_library(
        a-simple-triangle
        SHARED
        ${CPP_HEADERS}
        ${CPP_SOURCES}
        ${ANDROID_NDK}/sources/third_party/vulkan/src/common/vulkan_wrapper.cpp
)

# Finally, we need to link all the libraries we want to use in our project together, remembering that the 'a-simple-triangle' library is in fact our own C++ code. Note also we are including the 'GLESv2' library for OpenGL ES2 support, though we don't need to define it ourselves with the 'add_library' commands.
target_link_libraries(
        a-simple-triangle
        hidapi
        sdl2
        sdl2_image
        ${log-lib}
        GLESv2
)
